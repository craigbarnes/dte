#!/usr/bin/awk -f

function error(msg) {
    printf("%s:%d: Error: %s\n", FILENAME, FNR, msg)
    exit 1
}

# Ignore everything below "scissors" line (as used by "git commit -v")
/^# *--+ >8 --+$/ {
    exit 0
}

# Skip other commented lines
/^#/ {
    next
}

# Exclude long URLs from maximum line length check, so long as they
# appear at the start of a line (excluding an optional bullet point
# or numbered back-reference)
/^([*-]|\[[0-9]]:)? *https?:/ {
    if (++LINES <= 2) {
        error("this line shouldn't begin with a URL")
    }
    next
}

# Exclude Markdown-style indented code blocks (with 4 space indents)
# from maximum line length check
/^ {4,}[^ ]/ {
    if (++LINES <= 2) {
        error("this line shouldn't be indented")
    }
    next
}

/^.{73}/ {
    error("line exceeds 72 columns")
}

/^$/ {
    LINES++
    next
}

{
    LINES++
    if (LINES == 1 && $0 ~ /\. *$/) {
        error("first line shouldn't end with '.'")
    }
    if (LINES == 2) {
        error("second line should always be blank")
    }
}
