#!/usr/bin/awk -f

# Ignore everything below "scissors" line (as used by "git commit -v")
/^# *--+ >8 --+$/ {
    exit 0
}

# Skip other commented lines
/^#/ {
    next
}

# Exclude long URLs from maximum line length check, so long as they
# appear at the start of a line (excluding an optional bullet point
# or numbered back-reference)
/^([*-]|\[[0-9]]:)? *https?:/ {
    LINES++
    next
}

/^.{73}/ {
    printf("%s:%d: Line exceeds 72 columns\n", FILENAME, FNR)
    exit 1
}

/^$/ {
    LINES++
    next
}

{
    if (++LINES == 2) {
        printf("%s: line #2 should always be blank\n", FILENAME)
        exit 1
    }
}
